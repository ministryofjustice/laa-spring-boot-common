plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'uk.gov.laa.springboot'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(javaVersion))
}

dependencies {

    // Make sure we're using the same version of the Java plugin that we're adding into the starters
    implementation project(':laa-java-gradle-plugin')
    implementation "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    implementation "io.spring.gradle:dependency-management-plugin:$springBootDependencyManagementPluginVersion"
    implementation 'org.yaml:snakeyaml:2.2'

    testImplementation platform('org.junit:junit-bom:6.0.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'
            implementationClass = 'uk.gov.laa.gradle.springboot.LaaSpringBootGradlePlugin'
        }
        exportCodegenPlugin {
            id = 'uk.gov.laa.springboot.laa-spring-boot-starter-export-codegen-gradle-plugin'
            implementationClass = 'uk.gov.laa.gradle.springboot.starter.export.LaaSpringBootStarterExportCodegenGradlePlugin'
        }
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "LAA Spring Boot Conventions Plugin",
                "Implementation-Version": project.version
        )
    }
}
